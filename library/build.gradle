buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
}

version '0.0.6'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

}

android.buildTypes.all { buildType ->
    project.properties.any { property ->
        if (property.key.toLowerCase().startsWith("passcodelock.")) {
            buildType.buildConfigField "String", property.key.replace("passcodelock.", "").replace(".", "_").toUpperCase(), "\"${property.value}\""
        }
    }
}

task writePom << {
    pom {
        project {
            groupId "org.wordpress"
            artifactId "android-passcodelock"
            packaging "aar"
        }
    }.writeTo("$buildDir/pom.xml")
}
