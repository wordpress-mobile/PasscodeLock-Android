buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        versionName "1.2.0"
        minSdkVersion 9
        targetSdkVersion 23
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

android.buildTypes.all { buildType ->
    project.properties.any { property ->
        if (property.key.toLowerCase().startsWith("passcodelock.")) {
            buildType.buildConfigField "String", property.key.replace("passcodelock.", "").replace(".", "_").toUpperCase(), "\"${property.value}\""
        }
    }
}

signing {
    required {
        project.properties.containsKey("signing.keyId") && project.properties.containsKey("signing.secretKeyRingFile")
    }
    sign configurations.archives
}

version android.defaultConfig.versionName
group = "org.wordpress"
archivesBaseName = "passcodelock"

// http://central.sonatype.org/pages/gradle.html

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            pom.project {
                name 'PasscodeLock-Android'
                packaging 'aar'
                description 'Android Library that provides passcode lock to your app'
                url 'https://github.com/wordpress-mobile/PasscodeLock-Android'
                scm {
                    connection 'scm:git:https://github.com/wordpress-mobile/PasscodeLock-Android.git'
                    developerConnection 'scm:git:https://github.com/wordpress-mobile/PasscodeLock-Android.git'
                    url 'https://github.com/wordpress-mobile/PasscodeLock-Android'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'daniloercoli'
                        name 'Danilo Ercoli'
                        email 'danilo@automattic.com'
                    }
                    developer {
                        id 'tonyr59h'
                        name 'Anthony Rankin'
                        email 'tonyr@automattic.com'
                    }
                    developer {
                        id 'roundhill'
                        name 'Dan Roundhill'
                        email 'dan@automattic.com'
                    }
                    developer {
                        id 'maxme'
                        name 'Maxime Biais'
                        email 'maxime@automattic.com'
                    }
                }
            }
        }
    }
}
